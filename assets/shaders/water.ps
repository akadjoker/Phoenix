#version 300 es
precision highp float;

layout(location = 0) in vec3 aPos;
layout(location = 1) in vec3 aNormal;
layout(location = 2) in vec2 aTexCoord;

uniform mat4 model;
uniform mat4 view;              
uniform mat4 projection;        
uniform mat4 reflectionView;     

uniform float u_time;
uniform float u_waveLength;
uniform float u_windForce;
uniform vec2 u_windDirection;

out vec2 v_bumpMapTexCoord;
out vec3 v_refractionTexCoord;
out vec3 v_reflectionTexCoord;
out vec3 v_position3D;

void main()
{
    // Calcular worldViewProj dentro da shader
    mat4 worldViewProj = projection * view * model;
    
    vec4 pos = worldViewProj * vec4(aPos, 1.0);
    gl_Position = pos;
    
    v_bumpMapTexCoord = aTexCoord / u_waveLength + u_time * u_windForce * u_windDirection;

    // Refraction coords
    v_refractionTexCoord.x = 0.5 * (pos.w + pos.x);
    v_refractionTexCoord.y = 0.5 * (pos.w + pos.y);
    v_refractionTexCoord.z = pos.w;
    
    // Reflection coords (usa reflectionView)
    mat4 worldReflectionViewProj = projection * reflectionView * model;
    vec4 reflectPos = worldReflectionViewProj * vec4(aPos, 1.0);
    v_reflectionTexCoord.x = 0.5 * (reflectPos.w + reflectPos.x);
    v_reflectionTexCoord.y = 0.5 * (reflectPos.w + reflectPos.y);
    v_reflectionTexCoord.z = reflectPos.w;
    
    // World position
    vec4 worldPos = model * vec4(aPos, 1.0);
    v_position3D = worldPos.xyz;
}